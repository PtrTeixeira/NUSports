group 'github.PtrTeixeira'
version '0.1.0'

def versions = [
   checkstyle: '6.19',
   jsoup: '1.9.2',
   junit: '4.12',
   log4j: '2.6',
   testfx: '4.0.4-alpha'
]

configurations {
  checkstyleConfig
}

// Base plugin
apply plugin: 'java'
sourceCompatibility = 1.8

// Reporting Plugins
apply plugin: 'project-report'
apply plugin: 'build-dashboard'

apply plugin: 'checkstyle'
checkstyle {
    toolVersion = "${versions.checkstyle}"
}
tasks.withType(Checkstyle) {
  config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  reports {
    html.destination rootProject.file("build/reports/checkstyle.html")
  }
}

sourceSets {
  uiTest {
    java {
      compileClasspath += sourceSets.main.output
    }
  }
}

configurations {
  uiTestCompile.extendsFrom testCompile
  uiTestRuntime.extendsFrom testRuntime
}

task uiTest(type: Test) {
  testClassesDir = sourceSets.uiTest.output.classesDir
  classpath = sourceSets.uiTest.runtimeClasspath
  outputs.upToDateWhen { false }
}

check.dependsOn uiTest
uiTest.mustRunAfter test


tasks.withType(Test) {
  reports.html.destination = file("${reporting.baseDir}/${name}")
}

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'org.jsoup', name: 'jsoup', version: "${versions.jsoup}"
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${versions.log4j}"

  testCompile group: 'junit', name: 'junit', version: "${versions.junit}"

  checkstyleConfig module(group: 'com.puppycrawl.tools', name: 'checkstyle', version: "${versions.checkstyle}") {
    transitive = false
  }

//  uiTestCompile sourceSets.main.output
//  uiTestCompile configurations.testCompile
  testCompile group: 'org.testfx', name: 'testfx-core', version: "${versions.testfx}"
  testCompile group: 'org.testfx', name: 'testfx-junit', version: "${versions.testfx}"
}