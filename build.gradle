buildscript {
  ext.junit_version = '1.0.0-M4'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_version"
  }
}

import org.junit.platform.console.options.Details

plugins {
  // Language Plugins
  id 'java'
  id 'nebula.kotlin' version '1.1.3'

  // Reporting Plugins
  id 'project-report'
  id 'build-dashboard'

  // Because Intellij is not good with Dagger
  id 'idea'

  // Annotation Processing
  id 'net.ltgt.apt' version '0.5'
  id 'org.jetbrains.kotlin.kapt' version '1.1.3'

  // Validation plugins
  id 'com.diffplug.gradle.spotless' version '3.4.0'
}

apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.github.PtrTeixeira'
version '3.0.0'

def versions = [
    jsoup     : '1.10.3',
    junit     : '5.0.0-M4',
    log4j     : '2.8.2',
    assertj   : '3.8.0',
    spek      : '1.1.2',
    mockito   : '2.8.47',
    dagger    : '2.11',
    tornadofx : '1.7.7',
    coroutines: '0.16',
    awaitility: '3.0.0'
]

junitPlatform {
  platformVersion = '1.0.0-M4'
  filters {
    engines {
      include 'spek', 'junit-jupiter'
    }
  }
  details = Details.TREE
}

// Because Intellij plays poorly with Dagger
idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}

spotless {
  java {
    removeUnusedImports()
    googleJavaFormat()
    licenseHeaderFile 'LICENSE'
  }
  kotlin {
    ktlint()
    licenseHeaderFile 'LICENSE'
  }
  lineEndings 'UNIX'
}

compileKotlin {
  kotlinOptions {
    apiVersion = "1.1"
    jvmTarget = '1.8'
  }
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

kapt {
  correctErrorTypes = true
}

// Base plugin
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile group: 'org.jsoup', name: 'jsoup', version: versions.jsoup
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
  compile group: 'no.tornado', name: 'tornadofx', version: versions.tornadofx
  compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: versions.coroutines
  compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-javafx', version: versions.coroutines

  compile group: 'com.google.dagger', name: 'dagger', version: versions.dagger
  kapt group: 'com.google.dagger', name: 'dagger-compiler', version: versions.dagger

  testCompile group: 'org.assertj', name: 'assertj-core', version: versions.assertj
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.mockito', name: 'mockito-core', version: versions.mockito
  testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
}
