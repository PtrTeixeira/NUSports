buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'java'
  id 'nebula.kotlin' version '1.2.30'

  // Because Intellij is not good with Dagger
  id 'idea'

  id 'net.ltgt.apt' version '0.5'
  id 'com.diffplug.gradle.spotless' version '3.7.0'
  id 'application'
}

apply from: 'gradle/validation.gradle'
apply plugin: 'kotlin-kapt'

group 'com.github.PtrTeixeira'
version '3.1.1'

def versions = [
    jsoup     : '1.11.2',
    junit     : '5.1.0',
    log4j     : '2.10.0',
    assertj   : '3.9.1',
    mockito   : '2.15.0',
    dagger    : '2.15',
    tornadofx : '1.7.15',
    coroutines: '0.22.5'
]

// Because Intellij plays poorly with Dagger
idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}

distTar {
  compression = Compression.GZIP
}

compileKotlin {
  kotlinOptions {
    apiVersion = "1.2"
    jvmTarget = '1.8'
  }
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

kapt {
  correctErrorTypes = true
}

test {
  useJUnitPlatform()
}

sourceCompatibility = 1.8
mainClassName = 'com.github.ptrteixeira.nusports.SportsApp'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile group: 'org.jsoup', name: 'jsoup', version: versions.jsoup
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
  compile group: 'no.tornado', name: 'tornadofx', version: versions.tornadofx
  compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: versions.coroutines
  compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-javafx', version: versions.coroutines
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
  compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'

  compile group: 'com.google.dagger', name: 'dagger', version: versions.dagger
  kapt group: 'com.google.dagger', name: 'dagger-compiler', version: versions.dagger

  testCompile group: 'org.assertj', name: 'assertj-core', version: versions.assertj
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.mockito', name: 'mockito-core', version: versions.mockito
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
}
